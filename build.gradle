plugins {
	id 'application'
	id 'checkstyle'
	id 'jacoco'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'org.springdoc.openapi-gradle-plugin' version '1.3.3'
	id 'com.github.johnrengelman.processes' version '0.5.0'
	id 'com.github.ben-manes.versions' version '0.38.0'
	id 'org.siouan.frontend-jdk11' version '6.0.0'
	id 'org.springframework.boot' version '2.5.6'
	id 'com.adarshr.test-logger' version '2.1.1'
	id 'org.liquibase.gradle' version '2.0.4'
}

group = 'hexlet.code'
version = '1.0-SNAPSHOT'

application {
	mainClass = 'hexlet.code.AppApplication'
}

repositories {
	mavenCentral()
}

dependencies {
	compileOnly 'org.projectlombok:lombok:1.18.22'

	annotationProcessor(
			'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final',
			'javax.annotation:javax.annotation-api:1.3.2',
			'com.querydsl:querydsl-apt:5.0.0:jpa',
			'org.projectlombok:lombok:1.18.22',
	)

	implementation(
			'org.springframework.boot:spring-boot-starter-validation',
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'org.springframework.boot:spring-boot-starter-actuator',
			'org.springframework.boot:spring-boot-starter-security',
	        'org.springframework.boot:spring-boot-starter-web',
			'com.rollbar:rollbar-spring-boot-webmvc:1.8.1',
			'org.springdoc:springdoc-openapi-ui:1.5.12',
	        'org.liquibase:liquibase-core:4.6.1',
			'com.querydsl:querydsl-core:5.0.0',
			'com.querydsl:querydsl-jpa:5.0.0',
			'io.jsonwebtoken:jjwt:0.9.1'
	)

	liquibaseRuntime(
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'org.liquibase.ext:liquibase-hibernate5:4.5.0',
			'org.liquibase:liquibase-core:4.6.1',
			sourceSets.main.output
	)

	runtimeOnly (
			'org.postgresql:postgresql:42.2.24',
			'com.h2database:h2:1.4.200'
	)

	testImplementation(
			'org.springframework.security:spring-security-test:5.5.1',
			'org.springframework.boot:spring-boot-starter-security',
			'org.springframework.boot:spring-boot-starter-test',
			'com.tobedevoured.modelcitizen:spring:0.8.3'
	)
}

wrapper {
	gradleVersion = '7.4'
	distributionType = Wrapper.DistributionType.ALL
}

compileJava {
	options.release = 17
	options.encoding = 'UTF-8'
}

test {
	useJUnitPlatform()
}

diffChangeLog {
	dependsOn compileJava
}

liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/db/changelog/changelog-master.xml'
			url 'jdbc:h2:./taskManager'
			username ''
			password ''
			referenceUrl 'hibernate:spring:hexlet.code.model.' +
					'?dialect=org.hibernate.dialect.H2Dialect' +
					'&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy'
		}
	}
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}

checkstyle {
	toolVersion '10.0'
}

checkstyleMain.source = "src/main/java"

sourceSets.main.java.srcDirs = ['src/main/java', 'build/generated/sources/annotationProcessor/java/main']

// Сборка фронтенда.
// Выполняется только если ничего не изменилось.
// *****************************************************************************
frontend {
	nodeVersion = '16.13.1'
	installScript = 'install'
	assembleScript = 'run build'
	packageJsonDirectory = file("${projectDir}/frontend")
}

def configFrontendTasks = {
	inputs.files(fileTree("$projectDir/frontend").exclude('build', 'node_modules'))
	outputs.dir("$buildDir/resources/main/static")
}

assembleFrontend {
	configure configFrontendTasks

	doLast {
		copy {
			from "$projectDir/frontend/build"
			into "$buildDir/resources/main/static"
		}
	}
}

installFrontend {
	configure configFrontendTasks
}

processResources.dependsOn assembleFrontend
// *****************************************************************************